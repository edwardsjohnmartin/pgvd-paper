set term pdfcairo
set out 'compare2.pdf'

#set term pop
#set out

set title "Octree construction"
set boxwidth 0.9 absolute
set style fill   solid 1.00 border lt -1
set key inside right top vertical Right noreverse noenhanced autotitles nobox
set style histogram clustered gap 0 title offset character 1, 0, 0
set datafile missing '-'
set style data histograms

#set logscale y
unset logscale y
set yrange [0:*]

set ylabel "sec / Mb"

set xtics border in scale 0,0 nomirror rotate by 0  offset character 1, .3, 0 autojustify
set xtics  norangelimit font ",8"
set xtics   ()

# Color 1 and color 2
# See http://www.uni-hamburg.de/Wiss/FB/15/Sustainability/schneider/gnuplot/colors.htm for color listing
C1 = "#CD853F"
C2 = "#00FF00"

#plot "compare2.dat" using 1:3 title "LK11", using 1:4 title "Ours"
#plot "compare2.dat" using 4 lt 1
plot \
newhistogram "Neuron", \
  "<(sed -n '1,4p' compare2.dat)" using 3:xtic(2) ti col lc rgb C1, '' u 4 ti col lc rgb C2, \
newhistogram "Virus", \
  "<(sed -n '5,7p' compare2.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 4 lc rgb C2 notitle, \
newhistogram "Knives", \
  "<(sed -n '8,10p' compare2.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 4 lc rgb C2 notitle, \
newhistogram "Pipes", \
  "<(sed -n '11,13p' compare2.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 4 lc rgb C2 notitle

# close the file
set output
