unset multiplot
set term pdfcairo
set out 'compare.pdf'

#set term pop
#set out

# Set top and bottom margins to 0 so that there is no space between plots.
# Fix left and right margins to make sure that the alignment is perfect.
# Turn off xtics for all plots except the bottom one.

set tmargin 0
set bmargin 0
set multiplot layout 2,1 title "Octree construction\n"

set boxwidth 0.9 absolute
set style fill   solid 1.00 border lt -1
set key inside right top vertical Right noreverse noenhanced autotitles nobox
set style histogram clustered gap 0 title offset character 1, 0, 0
set datafile missing '-'
set style data histograms

TOP = 0.94
DY = 0.43

# Color 1 and color 2
# See http://www.uni-hamburg.de/Wiss/FB/15/Sustainability/schneider/gnuplot/colors.htm for color listing
C1 = "#CD853F"
C2 = "#00FF00"

#------------------------------------------------------------
# Top plot (time)
#------------------------------------------------------------
set tmargin at screen TOP
set bmargin at screen TOP-DY
set yrange [*:1000]
set ylabel "Time (sec)" offset 3
unset xtics
unset title
#set title "Octree construction"
set logscale y

plot \
newhistogram, \
  "<(sed -n '1,4p' compare.dat)" using 3:xtic(2) ti col lc rgb C1, '' u 5 ti col lc rgb C2, \
newhistogram, \
  "<(sed -n '5,7p' compare.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 5 lc rgb C2 notitle, \
newhistogram, \
  "<(sed -n '8,10p' compare.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 5 lc rgb C2 notitle, \
newhistogram, \
  "<(sed -n '11,13p' compare.dat)" using 3:xtic(2) lc rgb C1 notitle, '' u 5 lc rgb C2 notitle

#------------------------------------------------------------
# Bottom plot (memory)
#------------------------------------------------------------
set tmargin at screen TOP-DY
set bmargin at screen TOP-2*DY
set yrange [*:4000]
set ylabel "Memory (Mb)"
#set xtics border in scale 0,0 nomirror rotate by -45  offset character 0, 0, 0 autojustify
set xtics border in scale 0,0 nomirror rotate by 0  offset character 1, .3, 0 autojustify
set xtics  norangelimit font ",8"
set xtics   ()
unset key
unset title
set logscale y

plot \
newhistogram "Neuron", \
  "<(sed -n '1,4p' compare.dat)" using 4:xtic(2) ti col lc rgb C1, '' u 6 ti col lc rgb C2, \
newhistogram "Virus", \
  "<(sed -n '5,7p' compare.dat)" using 4:xtic(2) lc rgb C1 notitle, '' u 6 lc rgb C2 notitle, \
newhistogram "Knives", \
  "<(sed -n '8,10p' compare.dat)" using 4:xtic(2) lc rgb C1 notitle, '' u 6 lc rgb C2 notitle, \
newhistogram "Pipes", \
  "<(sed -n '11,13p' compare.dat)" using 4:xtic(2) lc rgb C1 notitle, '' u 6 lc rgb C2 notitle

unset multiplot

# close the file
set output

